apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.sonarqube'
apply plugin: 'nebula.rpm'
apply plugin: 'nebula.deb'
apply plugin: "com.github.hierynomus.license"
//apply plugin: 'nebula.lint'

def _vendor = 'Dell'
def _license = 'Apache License 2.0'
def _group = ''
def _url = 'http://support.dell.com'
def _arch = 'x86_64'
def _os = 'LINUX'
def _release = 1
def _sourcePaths=[]

sourceCompatibility = 1.8
version = "${version}"
dockerTag = "${dockerTag}"
group = 'com.dell.isg.smi'

buildscript {
	ext {
		springVersion = '4.3.6.RELEASE'
    	springBootVersion = '1.5.1.RELEASE'
    	springCloudVersion = 'Camden.SR5'
	}
   repositories {
    mavenLocal()
	maven {
		url "${artifactory_contextUrl}/libs-release"
		url "https://plugins.gradle.org/m2/"

	}
  }
  dependencies {
  	classpath "io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	classpath(group: 'com.netflix.nebula', name: 'gradle-ospackage-plugin', version: '4.4.0' )
	classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
	classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
	//classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
  }
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

//gradleLint {
//  rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

sonarqube {
    properties {
        property "sonar.projectName", "rackhd :: smi-service-virtualidentity"
        property "sonar.projectKey", "${group}:service-virtualidentity"
    }
}

springBoot {
    executable = true
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Virtual Identity Microservice',
                   'Implementation-Version': version
    }
    baseName = 'service-virtualidentity'
}

repositories {
	maven {
	    url "${artifactory_contextUrl}/${dependency_repo}"
	}
	mavenLocal()
}


dependencies {
    compile 'com.dell.isg.smi:commons-elm:1.0.76'
    compile 'com.dell.isg.smi:commons-utilities:1.0.26'
    compile 'com.dell.isg.smi:virtualidentity:1.0.35'
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile 'org.springframework.cloud:spring-cloud-starter-consul-all'

    compile "org.springframework.security.oauth:spring-security-oauth2"

    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'

	// needed for swagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1'
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1'

    // testCompile group: 'junit', name: 'junit', version: '4.+'
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

test {
    systemProperties 'property': 'value'
}


uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task generateDockerRpm(type: Rpm){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Exported service-virtualidentity image'
	description = 'Places the docker container tar.gz into tmp the directory'
	url = _url

	packageName = 'dell-smi-docker-service-virtualidentity'
	release = _release
	arch = _arch
	os = _os

    from fileTree( "tmp" ), {
        include "**/*"
        fileMode = 0755
	    user = "root"
	    addParentDirs = false
	    into  '/tmp'
    }

	from fileTree( "pkg/docker" ), {
        include "launch.sh"
        fileMode = 0755
	    //user = "root"
	    //addParentDirs = false
	    into  '/tmp'
    }
}

task generateRpm(type: Rpm){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Installs the Dell Virtual Identity spring boot application.'
	description = 'The Dell Virtual Identity spring boot application.'
	url = _url

	packageName = 'dell-smi-service-virtualidentity'
	release = _release
	arch = _arch
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = false
	    into  '/opt/dell/smi/service-virtualidentity'
    }

	from fileTree("./"), {
        include "application.yml"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/smi/service-virtualidentity'
    }

	from fileTree( "pkg" ), {
        include "dell-virtualidentity.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-virtualidentity.service', '/etc/systemd/system/dell-virtualidentity.service')
}

task generateDeb(type: Deb){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Installs the Dell Virtual Identity spring boot application.'
    description = 'The Dell Virtual Identity spring boot application.'
	url = _url

	packageName = 'dell-smi-service-virtualidentity'
	release = _release
	arch = 'all'
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = true
	    into  '/opt/dell/smi/service-virtualidentity'
    }

	from fileTree("./"), {
        include "application.yml"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/smi/service-virtualidentity'
    }

	from fileTree( "pkg" ), {
        include "dell-virtualidentity.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-virtualidentity.service', '/etc/systemd/system/dell-virtualidentity.service')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}


eclipse {
    project{
    	buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
    		
		// assign natures in a groovy fashion:
		natures = ['org.eclipse.buildship.core.gradleprojectnature']
    }
    
    classpath {
       downloadSources=true
    }
}


task buildSourcePathsArray(dependsOn: 'eclipseClasspath' ){
	doLast{
		def classpathFile = file('.classpath')
		if( classpathFile.exists() ) {
			def cpXml = new XmlParser().parse(classpathFile)
			cpXml.classpathentry.each {
				if('lib' == it.@kind ) {
					if( it.@sourcepath == null ){
						println 'missing source jar for:' + it.@path
					}
					else{
					 	_sourcePaths.add(it.@sourcepath)
					}
				}
			}
		}
		 
		def arrayLength = _sourcePaths.size()
		println 'sourcePaths length is:' + arrayLength
	}
}


task dependencySourcesZip(type: Zip, dependsOn: 'buildSourcePathsArray' ) {
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()
	baseName = "smi-service-virtualidentity-dependency-sources"
	version = "${dockerTag}"
	outputs.upToDateWhen { false }
 
    from _sourcePaths
}